plugins {
    id 'java-library'
    id 'maven-publish'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_7
    targetCompatibility = JavaVersion.VERSION_1_7
}

dependencies {
    implementation "org.jsonschema2pojo:jsonschema2pojo-core:1.1.1"
}

apply from: './versioning/version_tasks.gradle'

version = getAppVersionName()

project.ext.vstsUsername = System.getenv("ENV_VSTS_MVN_ANDROIDCOMMON_USERNAME") != null ? System.getenv("ENV_VSTS_MVN_ANDROIDCOMMON_USERNAME") : project.findProperty("vstsUsername")
project.ext.vstsPassword = System.getenv("ENV_VSTS_MVN_ANDROIDCOMMON_ACCESSTOKEN") != null ? System.getenv("ENV_VSTS_MVN_ANDROIDCOMMON_ACCESSTOKEN") : project.findProperty("vstsMavenAccessToken")

task sourcesJar(type: Jar) {
    from sourceSets.main.java.srcDirs
    classifier = 'sources'
    destinationDirectory = reporting.file("$project.buildDir/output/jars")
}

// Task to generate javadoc
task generateJavadoc(type: Javadoc) {
    failOnError false
    title = "Microsoft Identity Json2POJO Rules"
    source = sourceSets.main.java
    classpath += project.sourceSets.main.compileClasspath

    options.memberLevel = JavadocMemberLevel.PUBLIC
    options.addStringOption('Xdoclint:none', '-quiet')

    exclude '**/BuildConfig.Java'
    exclude '**/R.java'
    destinationDir = reporting.file("$project.buildDir/output/javadoc")
}

// Task to generate javadoc.jar
task javadocJar(type: Jar, dependsOn: generateJavadoc) {
    from javadoc.destinationDir
    classifier 'javadoc'
    destinationDirectory = reporting.file("$project.buildDir/output/jars")
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version)
    }
}

publishing {
    publications {
        aar(MavenPublication) {
            groupId 'com.microsoft.identity.json'
            artifactId 'rules'
            from components.java

            pom {
                name = 'common4j'
                description = 'This library contains rules for generating Java classes from Json ' +
                        'schema using the jsonschema2pojo library.'
                url = 'https://github.com/AzureAD/android-complete'
                developers {
                    developer {
                        id = 'microsoft'
                        name = 'Microsoft'
                    }
                }
                licenses {
                    license {
                        name = 'MIT License'
                    }
                }
                inceptionYear = '2021'
                scm {
                    url = 'https://github.com/AzureAD/android-complete'
                }
                properties = [
                        branch : 'master',
                        version: project.version
                ]
            }
        }
    }

    repositories {
        maven {
            name "vsts-maven-adal-android"
            url "https://identitydivision.pkgs.visualstudio.com/_packaging/AndroidADAL/maven/v1"
            credentials {
                username project.ext.vstsUsername
                password project.ext.vstsPassword
            }
        }
        maven {
            name "vsts-maven-android"
            url 'https://identitydivision.pkgs.visualstudio.com/IDDP/_packaging/Android/maven/v1'
            credentials {
                username project.vstsUsername
                password project.vstsPassword
            }
        }
    }
}

sourceSets {
    main {
        java.srcDirs = ['src/main']
    }
    test {
        java.srcDirs = ['src/test']
    }
}

